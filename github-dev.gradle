import com.smokejumperit.api.GitHubApi3

apply from:'http://smokejumperit.com/github-libs.gradle'

apply plugin:'maven'
apply plugin:'osgi'

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()	
		mavenRepo url:'http://repo.smokejumperit.com'
		mavenRepo url:'http://repository.codehaus.org'
	}
	dependencies {
		classpath 'RobertFischer:github-api-3:0.0.1'
	}
}

task ghWritePom {
	ext.pom = uploadArchives.repositories.mavenDeployer.pom
	ext.pomFile = File.createTempFile("pom", "xml")
	doLast {
		pom.writeTo(pomFile)
	}
}

task ghUpload(dependsOn:[tasks.ghWritePom, tasks.jar]) {
	ext.username = System.getProperty("github.username", System.getenv("GITHUB_USER")) ?: project.group ?: System.getProperty('user.name')
	ext.password = System.getProperty("github.password", System.getenv("GITHUB_PASS"))
	ext.repo = project.name

	onlyIf { 
		if(version.endsWith("-SNAPSHOT")) {
			logger.warn("Not uploading to GitHub because we're releasing a snapshot version")
			return false
		} else {
			return true
		}
	}

	onlyIf {
		if(!password) {
			logger.error(
				"Need password to upload to GitHub: set ghUpload task property 'password', Java property 'github.password', or system property 'GITHUB_PASS'"
			)
			return false
		} else {
			return true
		}
	}

	doLast {
		logger.info("Releasing to GitHub repository $username/$repo")
		def gitHub = new GitHubApi3(username, password, repo) 

		def fileBase = "$repo-$project.version"

/*
		// @TODO For some reason, deleting downloads isn't working, even though the URL is fine.
		// >> DELETE /repos/RobertFischer/github-api-3/downloads/220575 HTTP/1.1 -- Gives 404!
		def downloads = gitHub.listDownloads()
		downloads.findAll { it.name.startsWith(fileBase) }.each {
			logger.warn("Deleting download $it.id because its name starts with $fileBase: $it.name")
			gitHub.deleteDownload(it.id)
		}
*/

		def time = System.currentTimeMillis()
		logger.info("Uploading $tasks.jar.archivePath")
		gitHub.createDownload(tasks.jar.archivePath, "$project.name jar (${project.version}.${time})")
		logger.info("Uploading $tasks.ghWritePom.pomFile")
		gitHub.createDownload(tasks.ghWritePom.pomFile, "$project.name pom (${project.version}.${time})")
	}
}

tasks.uploadArchives.dependsOn tasks.ghUpload
