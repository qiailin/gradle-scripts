import com.smokejumperit.api.GitHubApi3

apply from:'http://smokejumperit.com/github-libs.gradle'

apply plugin:'java'
apply plugin:'maven'
apply plugin:'osgi'

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()	
		mavenRepo url:'http://repo.smokejumperit.com'
		mavenRepo url:'http://repository.codehaus.org'
	}
	dependencies {
		classpath 'RobertFischer:github-api-3:0.0.2'
	}
}

task ghWritePom(dependsOn:[configurations.archives.allArtifacts]) {
	ext.pom = uploadArchives.repositories.mavenDeployer.pom

	def tmpPom = File.createTempFile("pom", "xml")
	tmpPom.deleteOnExit()
	ext.pomFile = tmpPom

	doLast {
		// For some reason, this stuff wasn't being set appropriately
		pom.version = pom.version ?: project.version
		pom.artifactId = pom.artifactId ?: project.name
		pom.groupId = pom.groupId ?: project.group

		logger.info("Writing GitHub pomfile to ${pomFile.canonicalPath}")
		logger.debug("POM Version: ${pom.version ?: 'EMPTY'}")
		logger.debug("POM ArtifactId: ${pom.artifactId ?: 'EMPTY'}")
		logger.debug("POM GroupId: ${pom.groupId ?: 'EMPTY'}")
		pom.writeTo(pomFile)
		logger.debug("pom file:\n${pomFile.text}")
	}
}

task ghUpload(dependsOn:[configurations.archives.allArtifacts, tasks.ghWritePom]) {
	ext.username = System.getProperty("github.username", System.getenv("GITHUB_USER")) ?: project.group ?: System.getProperty('user.name')
	ext.password = System.getProperty("github.password", System.getenv("GITHUB_PASS"))
	ext.repo = project.name

	onlyIf {
		if(!password) {
			logger.error(
				"Need password to upload to GitHub: set ghUpload task property 'password', Java property 'github.password', or system property 'GITHUB_PASS'"
			)
			return false
		} else {
			return true
		}
	}

	doLast {
		logger.info("Releasing to GitHub repository $username/$repo")
		def gitHub = new GitHubApi3(username, password, repo) 

		def fileBase = "$repo-$project.version"

		def downloads = gitHub.listDownloads()
		downloads.findAll { it.name.startsWith(fileBase) }.each {
			logger.warn("Deleting download $it.id because its name starts with $fileBase: $it.name")
			it.delete()
		}

		def time = System.currentTimeMillis()
		def description = "$project.name $project.version $time (${new Date(time)})"
		configurations.archives.allArtifacts.files.each { File file ->
			logger.info("Uploading $file - $description")
			gitHub.createDownload(file, description)
		}

		logger.info("Uploading pom: ${project.name}-${project.version}.pom")
		gitHub.createDownload(tasks.ghWritePom.pomFile, "${project.name}-${project.version}.pom", "$project.name $project.version pom file ($time)")
	}
}

tasks.uploadArchives.dependsOn tasks.ghUpload
